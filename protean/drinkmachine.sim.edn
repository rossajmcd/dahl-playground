(ns drinkmachine.api
  (:require [dahl :as d]
            [protean.api.transformation.sim :as sim]))

(def entrypoint "api/drinkmachine")

(def atom-nodes (atom [{:Ready nil}]))

(def atom-edges (atom nil))

(def graph
  {:Ready {:select-ingredients :Ingredients}
   :Ingredients {:add-beverage :Ingredients
                 :add-milk :Ingredients
                 :add-sugar :Ingredients
                 :make-drink :MakingDrink}
   :MakingDrink {:shutdown :ShutDown
                 :complete-machine :MadeDrink}
   :MadeDrink {:shutdown :ShutDown}
   :Shutdown {}})

(def rules
   {:Ingredients (fn [n]
      {:Ingredients (dissoc (:Ingredients graph)
                            (when-not (get-in (last n) [:Ingredients :beverage])
                              :make-drink))})
    :MakingDrink (fn [_]
      (future (Thread/sleep 10000)
              (when (= (d/current-node-key @atom-nodes) :MakingDrink)
                (swap! atom-nodes d/update-node {:MadeDrink nil}))))})

(defn create-select
  [name label options]
  [:div {:class "form-group"}
    [:label {:for name} label]
    (concat
      [:select {:class "form-control" :id name :name name :required "required"}]
      [[:option {:value "" } (str "Select a " label)]]
      (for [x options] [:option {:value x} x]))])

(def bodies
  {:Ingredients {:add-beverage (create-select "beverage" "Beverage" ["tea" "coffee"])
                 :add-milk (create-select "milk" "Milk" ["none" "semi" "full"])
                 :add-sugar (create-select "sugar" "Sugar" [0 1 2])}})

(defn handler
  [req {:keys [status nodes edges result]}]
  (when nodes (reset! atom-nodes nodes))
  (when edges (reset! atom-edges edges))
  (sim/response req status result))

{
  "api" {
    "drinkmachine" {
      :get #(handler % (d/get entrypoint @atom-nodes graph rules bodies))
    }
    "drinkmachine/${uuid}" {
      :post #(handler % (d/post entrypoint @atom-nodes @atom-edges graph rules
                                bodies (sim/path-param % "uuid") (sim/body-clj % true)))
    }
  }
}
